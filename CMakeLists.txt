cmake_minimum_required(VERSION 3.17)
cmake_policy(SET CMP0091 NEW)  # ç¡®ä¿ MSVC è¿è¡Œæ—¶åº“ç­–ç•¥æ­£ç¡®

#-------------------------------------------------------------------------------
# é¡¹ç›®åŸºç¡€é…ç½®
#-------------------------------------------------------------------------------
project(TensorRT-YOLO
    VERSION 6.1.0
    LANGUAGES CXX CUDA
    DESCRIPTION "ğŸš€ Easier & Faster YOLO Deployment Toolkit for NVIDIA ğŸ› ï¸"
)

# ç”Ÿæˆç¼–è¯‘æ•°æ®åº“ï¼ˆä¾›clangdç­‰å·¥å…·ä½¿ç”¨ï¼‰
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# è®¾ç½® C++ æ ‡å‡†
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

#-------------------------------------------------------------------------------
# ä¾èµ–é…ç½®
#-------------------------------------------------------------------------------
# CUDA é…ç½®
find_package(CUDAToolkit REQUIRED)

# å…è®¸ç”¨æˆ·è¦†ç›–é»˜è®¤çš„ CUDA æ¶æ„
if(NOT DEFINED CMAKE_CUDA_ARCHITECTURES)
    set(CMAKE_CUDA_ARCHITECTURES "89;86;75;70;61")  # é»˜è®¤æ”¯æŒæ¶æ„
endif()

# TensorRT è·¯å¾„é…ç½®
set(TRT_PATH "" CACHE PATH "Path to TensorRT installation (e.g., /usr/local/tensorrt)")
if(NOT TRT_PATH)
    message(FATAL_ERROR "TensorRT path must be specified with -DTRT_PATH=/path/to/tensorrt")
endif()

# å®šä¹‰ TensorRT åº“çš„ç‰ˆæœ¬é€‰æ‹©
set(TRT_LIB_DIR "${TRT_PATH}/lib")
if(MSVC AND EXISTS "${TRT_LIB_DIR}/nvinfer_10.dll")
    set(TRT_LIBS nvinfer_10 nvinfer_plugin_10 nvonnxparser_10)
else()
    set(TRT_LIBS nvinfer nvinfer_plugin nvonnxparser)
endif()

# Python ç»‘å®šé€‰é¡¹
option(BUILD_PYTHON "Build Python bindings with pybind11" OFF)
if(BUILD_PYTHON)
    set(PYBIND11_FINDPYTHON ON)
    find_package(Python COMPONENTS Interpreter Development REQUIRED)
    find_package(pybind11 CONFIG REQUIRED)
endif()

#-------------------------------------------------------------------------------
# ç¼–è¯‘å·¥å…·é“¾é…ç½®
#-------------------------------------------------------------------------------
function(set_target_compile_options target)
    # MSVC é…ç½®
    if(MSVC)
        # C++ç¼–è¯‘é€‰é¡¹
        target_compile_options(${target} PRIVATE
            $<$<AND:$<CONFIG:Release>,$<COMPILE_LANGUAGE:CXX>>:/O2 /Oi /fp:fast>
            $<$<AND:$<CONFIG:Debug>,$<COMPILE_LANGUAGE:CXX>>:/Od /Ob0 /Zi /RTC1>
        )

        # CUDAç¼–è¯‘é€‰é¡¹
        target_compile_options(${target} PRIVATE
            $<$<AND:$<CONFIG:Release>,$<COMPILE_LANGUAGE:CUDA>>:-Xcompiler=/O2 -O3>
            $<$<AND:$<CONFIG:Debug>,$<COMPILE_LANGUAGE:CUDA>>:-Xcompiler=/Od -g -G>
        )

        set_target_properties(${target} PROPERTIES MSVC_RUNTIME_LIBRARY
            "$<$<CONFIG:Debug>:MultiThreadedDebugDLL>$<$<CONFIG:Release>:MultiThreadedDLL>"
        )

    # GCC/Clang é…ç½®
    else()
        # C++ç¼–è¯‘é€‰é¡¹
        target_compile_options(${target} PRIVATE
            $<$<AND:$<CONFIG:Release>,$<COMPILE_LANGUAGE:CXX>>:-O3 -march=native -flto=auto -DNDEBUG>
            $<$<AND:$<CONFIG:Debug>,$<COMPILE_LANGUAGE:CXX>>:-O0 -g3 -fno-omit-frame-pointer -fno-inline>
        )

        # CUDAç¼–è¯‘é€‰é¡¹
        target_compile_options(${target} PRIVATE
            $<$<COMPILE_LANGUAGE:CUDA>:-Xcompiler=-Wno-deprecated-declarations>
            $<$<AND:$<CONFIG:Release>,$<COMPILE_LANGUAGE:CUDA>>:-O3>
            $<$<AND:$<CONFIG:Debug>,$<COMPILE_LANGUAGE:CUDA>>:-g -G>
        )

        target_link_options(${target} PRIVATE
            $<$<AND:$<CONFIG:Release>,$<COMPILE_LANGUAGE:CXX>>:-O3 -flto=auto>
            $<$<AND:$<CONFIG:Debug>,$<COMPILE_LANGUAGE:CXX>>:-g3>
        )
    endif()

    # è·¨å¹³å°å®å®šä¹‰
    target_compile_definitions(${target} PRIVATE
        $<$<CONFIG:Debug>:DEBUG>
        $<$<NOT:$<CONFIG:Debug>>:NDEBUG>
    )
endfunction()

#-------------------------------------------------------------------------------
# TensorRT/CUDA ç›®æ ‡é…ç½®
#-------------------------------------------------------------------------------
function(configure_cuda_trt_target target)
    # åŒ…å«ç›®å½•
    target_include_directories(${target} PRIVATE
        ${TRT_PATH}/include
    )

    # é“¾æ¥ç›®å½•
    target_link_directories(${target} PRIVATE
        ${TRT_LIB_DIR}
    )

    # æ·»åŠ é“¾æ¥åº“
    target_link_libraries(${target} PRIVATE
        CUDA::cudart
        ${TRT_LIBS}
    )

    # CUDA ç‰¹æ€§
    set_target_properties(${target} PROPERTIES
        CUDA_SEPARABLE_COMPILATION ON
        CUDA_RESOLVE_DEVICE_SYMBOLS ON
    )
endfunction()

#-------------------------------------------------------------------------------
# æ”¶é›†æºæ–‡ä»¶ï¼ˆä½¿ç”¨ CONFIGURE_DEPENDS è‡ªåŠ¨æ›´æ–°ï¼‰
#-------------------------------------------------------------------------------
function(add_target_compile_files target)
    # æ·»åŠ é¡¹ç›®æºç ç›®å½•åˆ°ç›®æ ‡çš„ç§æœ‰åŒ…å«ç›®å½•
    target_include_directories(${target} PRIVATE ${PROJECT_SOURCE_DIR})

    # æ”¶é›†æºæ–‡ä»¶
    file(GLOB_RECURSE SOURCES CONFIGURE_DEPENDS
        "${PROJECT_SOURCE_DIR}/deploy/core/*.cpp"
        "${PROJECT_SOURCE_DIR}/deploy/utils/*.cpp"
        "${PROJECT_SOURCE_DIR}/deploy/infer/*.cpp"
        "${PROJECT_SOURCE_DIR}/deploy/infer/*.cu"
        "${PROJECT_SOURCE_DIR}/deploy/model.cpp"
    )

    # å°†æ”¶é›†åˆ°çš„æºæ–‡ä»¶æ·»åŠ åˆ°ç›®æ ‡
    target_sources(${target} PRIVATE ${SOURCES})
endfunction()

#-------------------------------------------------------------------------------
# ä¸»åº“ç›®æ ‡
#-------------------------------------------------------------------------------
# åˆ›å»ºä¸»åº“
add_library(deploy SHARED)
add_target_compile_files(deploy)
configure_cuda_trt_target(deploy)
set_target_compile_options(deploy)

# æ¨¡å—é…ç½®
set_target_properties(deploy PROPERTIES
    OUTPUT_NAME "deploy"
    LIBRARY_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib"
    ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib"
    RUNTIME_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/lib"
    LIBRARY_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/lib"
    ARCHIVE_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/lib"
    RUNTIME_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/lib"
    LIBRARY_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/lib"
    ARCHIVE_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/lib"
    CXX_VISIBILITY_PRESET "hidden"
)

#-------------------------------------------------------------------------------
# Python ç»‘å®šï¼ˆå¯é€‰ï¼‰
#-------------------------------------------------------------------------------
if(BUILD_PYTHON)
    pybind11_add_module(pydeploy ${PROJECT_SOURCE_DIR}/deploy/pybind.cpp)
    add_target_compile_files(pydeploy)
    configure_cuda_trt_target(pydeploy)
    set_target_compile_options(pydeploy)

    # Python æ¨¡å—é…ç½®
    set_target_properties(pydeploy PROPERTIES
        OUTPUT_NAME "pydeploy"
        LIBRARY_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/python/tensorrt_yolo/libs"
        LIBRARY_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/python/tensorrt_yolo/libs"
        LIBRARY_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/python/tensorrt_yolo/libs"
        CXX_VISIBILITY_PRESET "hidden"
    )

    # Python é…ç½®æ–‡ä»¶
    configure_file(
        ${PROJECT_SOURCE_DIR}/python/tensorrt_yolo/c_lib_wrap.py.in
        ${PROJECT_SOURCE_DIR}/python/tensorrt_yolo/c_lib_wrap.py
    )
endif()

#-------------------------------------------------------------------------------
# å­é¡¹ç›®
#-------------------------------------------------------------------------------
add_subdirectory(plugin)